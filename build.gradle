plugins {
    id 'java'
}

group 'org.example'
version '1.0'

sourceCompatibility = 14
targetCompatibility = 14

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes "Main-Class": "com.artemget.oil_service.Application"
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    duplicatesStrategy(DuplicatesStrategy.WARN)
    with jar
}

artifacts {
    archives fatJar
}

dependencies {
    def vertxVersion = '4.1.0'
    def guiceVersion = '5.0.1'
    def apachePOIVersion = '5.0.0'
    def jacksonVersion = '2.12.3'

    implementation group: 'com.google.inject', name: 'guice', version: guiceVersion
    implementation group: 'io.vertx', name: 'vertx-core', version: vertxVersion
    implementation group: 'io.vertx', name: 'vertx-web', version: vertxVersion
    implementation group: 'io.vertx', name: 'vertx-auth-jwt', version: vertxVersion
    implementation group: 'io.vertx', name: 'vertx-web-validation', version: vertxVersion
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: apachePOIVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jacksonVersion
    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    implementation group: 'org.jdbi', name: 'jdbi3-core', version: '3.20.1'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.22'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.31'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.11.1'
    testImplementation group: 'io.vertx', name: 'vertx-web-client', version: vertxVersion

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}